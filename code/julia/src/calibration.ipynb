{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "# Calibration with moments\n",
    "The average wage at occupation $j$ is $$\\mathcal M^{theo}_{1j} =\\frac{\\int w_{ij}n_{ij}di}{\\int n_{ij}di}.$$ \n",
    "\n",
    "We do not observe type $i$, but the occupation (current or previous). We define $u_{ij_0}$ the measure of unemployed workers previously employed in occupation $j_0$.\n",
    "\\begin{align}\n",
    "    & u_{ij_0}=\\frac{\\delta_{j_0} n_{ij_0}}{\\int s(e^0_{ij_1})m_{j_1}dj_1}\n",
    "\\end{align}\n",
    "We can define i) the (instantaneous) probability of workers initially unemployed from occupation $j_0$ to obtain a job in occupation $j_1$, and ii) the probability of workers coming from occupation $j_0$ to obtain a job in occupation $j_1$.\n",
    "\\begin{align}\n",
    "    & \\mathcal M^{theo}_{2j_0j_1} =  \\frac{\\int s(e^0_{ij_1})m_{j_1}u_{ij_0}di}{\\int u_{ij_0}di}\\\\\n",
    "    & \\mathcal M^{theo}_{3j_0j_1} = \\frac{\\int \\xi s(e^{j_0}_{ij_1})m_{j_1}n_{ij_0}di}{\\int n_{ij_0}di}\\\\\n",
    "\\end{align}\n",
    "Mom 4?\n",
    "$$\\mathcal M^{theo}_{4j} = \\int n_{ij}di$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function compute_uu(A::Alloc,M::Para,N::Hyper)\n",
    "    uu = zeros(N.I,N.J)\n",
    "    for i in 1:N.I\n",
    "        denom = 0.\n",
    "        for j1 in 1:N.J\n",
    "            denom += s(A.e0[i,j1],M) * m(A.theta[j1],M)\n",
    "        end\n",
    "        \n",
    "        for j0 in 1:N.J \n",
    "            uu[i,j0] = M.delta[j0] * A.n[i,j0] / denom  \n",
    "        end\n",
    "    end\n",
    "    return(uu)\n",
    "end\n",
    "\n",
    "function theo_moments(A::Alloc,T::Taxes,M::Para,N::Hyper;\n",
    "    betatilde = M.beta / (M.beta + (1-M.beta)*(1+T.w)))\n",
    "        \n",
    "    uu = compute_uu(A,M,N)\n",
    "    mom1 = zeros(N.J)\n",
    "    mom2 = zeros(N.J,N.J)\n",
    "    mom3 = zeros(N.J,N.J)\n",
    "    mom4 = zeros(N.J)\n",
    "    \n",
    "    \n",
    "    for j0 in 1:N.J\n",
    "        uj0 = 0.\n",
    "        for i in 1:N.I\n",
    "            mom4[j0] += A.n[i,j0]\n",
    "            uj0 += uu[i,j0]\n",
    "            mom1[j0] += wage(i,j0,A,T,M,N,betatilde=betatilde) * A.n[i,j0]\n",
    "        end\n",
    "        mom1[j0] /= mom4[j0]\n",
    "        \n",
    "        for j1 in 1:N.J\n",
    "            for i in 1:N.I\n",
    "                mom2[j0,j1] += s(A.e0[i,j1],M) * m(A.theta[j1],M) * uu[i,j0]\n",
    "                mom3[j0,j1] += M.xi * s(A.e1[i,j0,j1],M) * m(A.theta[j1],M) * A.n[i,j0]\n",
    "            end\n",
    "            uj0 = uj0>0 ? uj0 : 1e2 ## penality if uj0==0 \n",
    "            mom2[j0,j1] /= uj0\n",
    "            mom3[j0,j1] /= mom4[j0]\n",
    "            #if j0==j1\n",
    "              #  mom3[j0,j1] = 0. ## in the theory e1[j0,j0]=0 so mom3[j0,j0]=0\n",
    "            #end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return(Moments(mom1, mom2, mom3, mom4) )\n",
    "end\n",
    "\n",
    "\n",
    "function build_weights(emp::Moments)\n",
    "    m1 = mean(emp.mom1)\n",
    "    m2 = mean(emp.mom2)\n",
    "    m4 = mean(emp.mom4)\n",
    "    mycount = 0.\n",
    "    mysum = 0.\n",
    "    for j0 in 1:length(emp.mom1)\n",
    "        for j1 in 1:(j0-1)\n",
    "            mysum += emp.mom3[j0,j1] + emp.mom3[j1,j0]\n",
    "            mycount += 2\n",
    "        end\n",
    "    end\n",
    "    m3 = mysum/mycount\n",
    "    return( [1/m1,1/m2,1/m3,1/m4] )\n",
    "end  \n",
    "\n",
    "\n",
    "function distance_mom(d1::Moments,d2::Moments, wgt, N::Hyper)\n",
    "    totmom1 = sum(abs2,(d1.mom1-d2.mom1) * wgt[1]) / N.J\n",
    "    totmom2 = sum(abs2,(d1.mom2-d2.mom2) * wgt[2]) / (N.J*N.J)\n",
    "    totmom3 = 0\n",
    "    for j0 in 1:N.J\n",
    "        for j1 in 1:(j0-1)\n",
    "            totmom3 += ((d1.mom3[j0,j1]-d2.mom3[j0,j1]) * wgt[3])^2\n",
    "            totmom3 += ((d1.mom3[j1,j0]-d2.mom3[j1,j0]) * wgt[3])^2\n",
    "        end\n",
    "    end\n",
    "    totmom3 /= N.J*(N.J-1) \n",
    "    totmom4 = sum(abs2,(d1.mom4-d2.mom4) * wgt[4]) / N.J\n",
    "    return(totmom1+totmom2+totmom3+totmom4)\n",
    "end\n",
    "distance_mom(par::ParCalib, A,T,M, N::Hyper) = distance_mom(par.emp,theo_moments(A,T,M,N),par.wgt,N)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We take the parametric structure for $y_{ij}$ with $0\\leq \\alpha_j\\leq 1$\n",
    "$$\n",
    "y_{ij}=(1-\\alpha_j \\mathbf{1}_{i\\ne j})y_{jj}+\\alpha_j \\mathbf{1}_{i\\ne j} h\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function build_y(yhigh,alpha,h)\n",
    "    y = zeros(length(yhigh),length(yhigh)) + Diagonal(yhigh)\n",
    "    for j0 in 1:length(yhigh)\n",
    "        for j1 in 1:(j0-1)\n",
    "            y[j0,j1] = (1-alpha[j1]) * yhigh[j1] + alpha[j1] * h\n",
    "            y[j1,j0] = (1-alpha[j0]) * yhigh[j0] + alpha[j0] * h\n",
    "        end\n",
    "    end\n",
    "    return(y)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 1) Calibrate a segmented allocation\n",
    "We can calibrate a segmented allocation based on a subset of moments.\n",
    "We look for $y_{jj}$ and vacancy costs $k_j$.\n",
    "We avoid computing $\\theta_j$ through a fixed-point algorithm by minimising the moment distance in $y_{jj}$ and $\\theta_j$.\n",
    "There are as many parameters as unknown, so we expect the distance to reach 0.\n",
    "\n",
    "\n",
    "We choose an absolute tolerance: 0.1 for $y_{jj}$ and 0.001 for $\\nu_i$ (we put a large number for $\\theta$ so that it is not binding)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function objective_calibrate_segm_equilibrium(vec,\n",
    "        par::ParCalib,T::Taxes,M::Para,N::Hyper;\n",
    "        betatilde=M.beta / (M.beta + (1-M.beta)*(1+T.w)) )\n",
    "    ## vec = [ydiag,theta,nuc]\n",
    "    \n",
    "    (A1,M1) = compute_segm_equilibrium_theta(vec,T,M,N,betatilde=betatilde)\n",
    "    out = distance_mom(par, A1,T,M1,N)\n",
    "    return(out)\n",
    "end\n",
    "\n",
    "\n",
    "function calibrate_segm_equilibrium(par::ParCalib, T::Taxes, M::Para,N::Hyper; \n",
    "        betatilde=M.beta / (M.beta + (1-M.beta)*(1+T.w)),  \n",
    "        toestim=[true,true,true], ## whether to estimate yd, theta and nu\n",
    "        verbose= false, maxti=600, algo=:LN_SBPLX) \n",
    "    \n",
    "    ## initialise vec = [ydiag,theta,nuc]\n",
    "    ydiag0 = par.emp.mom1 * 3\n",
    "    theta0 = fill(1e-5,N.J)\n",
    "    nuc0 = nu2nuc(par.emp.mom4 ./ sum(par.emp.mom4 ))\n",
    "    vec0 = vcat(ydiag0, theta0, nuc0)\n",
    "\n",
    "    ## parameters for optimization\n",
    "    opt = Opt(algo, length(vec0)) \n",
    "    maxtime!(opt,maxti)\n",
    "    xtol_abs!(opt, vcat(fill(0.1,N.J), fill(1,N.J), fill(0.001/((N.I-1)*0.01),N.I-1 )))\n",
    "    \n",
    "    # bounds\n",
    "    lbd = par.lbd\n",
    "    ubd = par.ubd\n",
    "    lbd_vec = vcat(fill(lbd.y,N.J), fill(lbd.theta,N.J), fill(lbd.nuc,N.I-1 ))\n",
    "    ubd_vec = vcat(fill(ubd.y,N.J), fill(ubd.theta,N.J), fill(ubd.nuc,N.I-1) )\n",
    "    toestim_vec = vcat(fill(toestim[1],N.J), fill(toestim[2],N.J), fill(toestim[3],N.I-1) )\n",
    "    lower_bounds!(opt, toestim_vec .* lbd_vec + (1 .- toestim_vec) .* vec0 )\n",
    "    upper_bounds!(opt, toestim_vec .* ubd_vec + (1 .- toestim_vec) .* vec0 )\n",
    "    \n",
    "    function obj(vec,g) \n",
    "        out = objective_calibrate_segm_equilibrium(vec,par,T,M,N,  betatilde=betatilde)\n",
    "        verbose ? print(out,\" \") : ()\n",
    "        return(out)\n",
    "    end\n",
    "    min_objective!(opt,obj)\n",
    "\n",
    "    (minf,vec,ret) = optimize(opt,vec0)\n",
    "    println(\"The minimum is reached at \",minf, \" with \", ret) \n",
    "  \n",
    "    ## output\n",
    "    (A1,M1) = compute_segm_equilibrium_theta(vec,T,M,N,betatilde=betatilde)    \n",
    "    return(A1, M1)\n",
    "    \n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 2) Full calibration\n",
    "We look for $y_{jj}$, $\\alpha_j$, $\\epsilon$, $\\xi$ and $\\theta_j$.\n",
    "We then compute the $k_j$ that give the market tightness $\\theta_j$ at equilibrium."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function objective_calibrate_equilibrium(vec, par::ParCalib,T::Taxes,M::Para,N::Hyper;\n",
    "    betatilde = M.beta / (M.beta + (1-M.beta)*(1+T.w)),\n",
    "    Xtol=1e-2)\n",
    "    ## vec=[yhigh,alpha,es,xi,theta,nuc]\n",
    "    \n",
    "    (A1,M1) = compute_equilibrium_theta(vec,T,M,N,betatilde=betatilde,Xtol=Xtol)\n",
    "    out = distance_mom(par, A1,T,M1,N)    \n",
    "    return(out)\n",
    "end\n",
    "\n",
    "\n",
    "\n",
    "function calibrate_equilibrium(par::ParCalib,T::Taxes,M::Para,N::Hyper ;\n",
    "        betatilde = M.beta / (M.beta + (1-M.beta)*(1+T.w)),\n",
    "        A0 = compute_segm_equilibrium(T,M,N),\n",
    "        toestim= fill(true,6), ##whether to estimate each of the 6: yd, alpha, es, xi, theta, nu\n",
    "        Xtol=1e-2, verbose = false, maxti= 600, algo=:LN_SBPLX) # :LN_BOBYQA\n",
    "         \n",
    "    lbd = par.lbd\n",
    "    ubd = par.ubd\n",
    "    \n",
    "    ## initialisation vec=[yhigh,alpha,es,xi,theta,nuc]\n",
    "    alpha0 = vcat( (M.y[1,1]-M.y[2,1])/(M.y[1,1]-M.h), \n",
    "        [(M.y[i,i]-M.y[1,i])/(M.y[i,i]-M.h) for i in 2:N.J]  )\n",
    "    vec0 = vcat(diag(M.y), min.(alpha0,ubd.alpha), M.es, M.xi, \n",
    "        max.(A0.theta,lbd.theta), nu2nuc(M.nu))\n",
    "\n",
    "    ## parameters for optimization\n",
    "    opt = Opt(algo, length(vec0)) \n",
    "    maxtime!(opt,maxti)\n",
    "    \n",
    "    lbd = par.lbd\n",
    "    ubd = par.ubd\n",
    "    lbd_vec = vcat(fill(lbd.y,N.J), fill(lbd.alpha,N.J), lbd.es, lbd.xi, \n",
    "                        fill(lbd.theta,N.J), fill(lbd.nuc,N.I-1 ) )\n",
    "    ubd_vec = vcat(fill(ubd.y,N.J), fill(ubd.alpha,N.J), ubd.es, ubd.xi,\n",
    "                        fill(ubd.theta,N.J), fill(ubd.nuc,N.I-1 ) )\n",
    "    toestim_vec = vcat(fill(toestim[1],N.J), fill(toestim[2],N.J), \n",
    "        toestim[3], toestim[4], fill(toestim[5],N.J), fill(toestim[6],N.I-1) )\n",
    "    lower_bounds!(opt, toestim_vec .* lbd_vec + (1 .- toestim_vec) .* vec0 )\n",
    "    upper_bounds!(opt, toestim_vec .* ubd_vec + (1 .- toestim_vec) .* vec0 )\n",
    "    \n",
    "    function obj(vec,g) \n",
    "        #println(\"par: \",vec[N.J*2+1], \" \", vec[N.J*2+2],\" \", vec[N.J*2+3])\n",
    "        out = objective_calibrate_equilibrium(vec,\n",
    "            par,T,M,N,Xtol=Xtol,  betatilde=betatilde)\n",
    "        verbose ? print(out,\" \") : ()\n",
    "        return(out)\n",
    "    end\n",
    "    min_objective!(opt,obj)\n",
    "\n",
    "    (minf,vec,ret) = optimize(opt,vec0)\n",
    "    println(\"The minimum is reached at \",minf, \" with \", ret)\n",
    "\n",
    "    ## output\n",
    "    (A1,M1) = compute_equilibrium_theta(vec,T,M,N,betatilde=betatilde,Xtol=Xtol)\n",
    "    return(A1, M1)\n",
    "end\n",
    "\n",
    "\n",
    "## calibrate with a fix alpha (uniform) and xi\n",
    "function calibrate_equilibrium_step1(par::ParCalib,T::Taxes,M::Para,N::Hyper ;\n",
    "        betatilde = M.beta / (M.beta + (1-M.beta)*(1+T.w)),\n",
    "        A0 = compute_segm_equilibrium(T,M,N),\n",
    "        Xtol=1e-2, verbose = false, maxti= 600, algo=:LN_SBPLX) # :LN_BOBYQA\n",
    "         \n",
    "    ## initialisation vec=[alpha0,xi]\n",
    "    alpha0 = (M.y[1,1]-M.y[2,1])/(M.y[1,1]-M.h)\n",
    "    axi0 = [alpha0,M.xi]\n",
    "\n",
    "    ## parameters for optimization\n",
    "    opt = Opt(algo, 2) \n",
    "    maxtime!(opt,maxti)\n",
    "    lower_bounds!(opt, [par.lbd.alpha, par.lbd.xi] )\n",
    "    upper_bounds!(opt, [par.ubd.alpha, par.ubd.xi] )\n",
    "    \n",
    "    function obj(axi,g) \n",
    "        vec = vcat(diag(M.y), fill(axi[1],N.J), M.es, axi[2], \n",
    "                    max.(A0.theta,par.lbd.theta), nu2nuc(M.nu))\n",
    "        out = objective_calibrate_equilibrium(vec,\n",
    "                    par,T,M,N,Xtol=Xtol,  betatilde=betatilde)\n",
    "        verbose ? print(out,\" \") : ()\n",
    "        return(out)\n",
    "    end\n",
    "    min_objective!(opt,obj)\n",
    "\n",
    "    (minf,axi,ret) = optimize(opt,axi0)\n",
    "    println(\"The minimum is reached at \",minf, \" with \", ret)\n",
    "\n",
    "    ## output\n",
    "    vec = vcat(diag(M.y), fill(axi[1],N.J), M.es, axi[2], \n",
    "            max.(A0.theta,par.lbd.theta), nu2nuc(M.nu))\n",
    "    (A1,M1) = compute_equilibrium_theta(vec,T,M,N,betatilde=betatilde,Xtol=Xtol)\n",
    "    return(A1, M1)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We directly compute the vacancy costs defined by:\n",
    "\\begin{align*}\n",
    "    & k_{j_1} = q(\\theta_{j_1}) (1-\\tilde\\beta)\\frac{num_{j_1}}{den_{j_1}}\\\\\n",
    "    & num_{j_1}=\\int_{\\mathcal I\\times\\mathcal J}\\left( X_{ij_1}-X_{ij_0}-TT^{j_0}_{ij_1}\\right)\\xi s(e_{ij_1}^{j_0})n_{ij_0}didj_0\n",
    "     +\\int_\\mathcal{I}\\left(X_{ij_1}-T_{ij_1}\\right) s(e_{ij_1}^{0}) u_idi\\\\\n",
    "     & den_{j_1}=\\int_{\\mathcal I\\times\\mathcal J}\\xi s(e_{ij_1}^{j_0})n_{ij_0}didj_0\n",
    "     +\\int_\\mathcal{I}s(e_{ij_1}^{0}) u_idi\n",
    "\\end{align*}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function compute_k(A::Alloc,T::Taxes,M::Para,N::Hyper;\n",
    "    betatilde = M.beta / (M.beta + (1-M.beta)*(1+T.w)))\n",
    "\n",
    "    k = zeros(N.J)\n",
    "    for j1 in 1:N.J\n",
    "        den = 0.\n",
    "        num = 0.\n",
    "        for i in 1:N.I\n",
    "            ## p = 0\n",
    "            num +=  (A.X[i,j1] - T_tax(i,j1,A.X,T,M)) * s(A.e0[i,j1],M) * A.u[i]\n",
    "            den += s(A.e0[i,j1],M) * A.u[i]\n",
    "            \n",
    "            ## p =j0\n",
    "            for j0 in 1:N.J\n",
    "                num +=  (A.X[i,j1] - A.X[i,j0] - TT_tax(i,j0,j1,A.X,T,M)) * M.xi * s(A.e1[i,j0,j1],M) * A.n[i,j0]\n",
    "                den += M.xi * s(A.e1[i,j0,j1],M) * A.n[i,j0]\n",
    "            end\n",
    "        end\n",
    "        k[j1] = q(A.theta[j1],M) * (1-betatilde) * num/den\n",
    "    end \n",
    "    \n",
    "    return(k)\n",
    "end\n"
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.0.5",
   "language": "julia",
   "name": "julia-1.0"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.0.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
